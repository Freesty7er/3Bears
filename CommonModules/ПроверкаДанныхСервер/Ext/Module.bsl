Функция ОбъектИзменён(объект) Экспорт
	
	Если ПроверитьИзменениеОбъекта(объект) Тогда
	
		объект.ДополнительныеСвойства.Вставить("ОбъектИзменён", Истина);
		Возврат Истина;
	
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции // ОбъектИзменён()

// Функция выполняет непосредственную проверку объекта на наличие изменений. Является вспомогательной
// функцией для функции ОбъектИзменён().
//
// Параметры
//  Объект  - <Тип объекта>Объект.<Имя объекта> - Любой объект, хранящийся в базе данных и имеющий
//                 объектно-ссылочную природу.
//
// Возвращаемое значение:
//   Булево   - Истина - данные объекта изменились по сравнению с данными, записанными в базе данных;
//                 Ложь - данные объекта не изменились.
//
Функция ПроверитьИзменениеОбъекта(объект)

	объектИзменён = Ложь;
	
	// Если объект уже проверялся на изменение, то повторную проверку делать смысла нет. Флаг
	// изменённости объекта будет сохранён до тех пор, пока объект не будет считан или создан
	// заново.
	Если объект.ДополнительныеСвойства.Свойство("ОбъектИзменён", объектИзменён) И объектИзменён Тогда
		Возврат Истина;
	КонецЕсли; 

	// Проверяем соответствие ранее сохранённому состоянию объекта.
	записьXML = Новый ЗаписьXML;
	
	// Получим XML-слепок нового состояния объекта.
	записьXML.УстановитьСтроку();
	ЗаписатьXML(ЗаписьXML, Объект);
	новоеСостояниеОбъекта = записьXML.Закрыть();
	
	исходноеСостояниеОбъекта = Неопределено;
	Если объект.ДополнительныеСвойства.Свойство("ИсходноеСостояниеОбъекта", исходноеСостояниеОбъекта) Тогда
		объектИзменён = исходноеСостояниеОбъекта <> новоеСостояниеОбъекта;
	Иначе
		
		объектИзменён = Ложь;
		объект.ДополнительныеСвойства.Вставить("ИсходноеСостояниеОбъекта", новоеСостояниеОбъекта);
		
	КонецЕсли; 
	
	// Сравним объект с его же записанным в базу данных состоянием.
	Если Не объектИзменён Тогда
		
		Если Не объект.Ссылка.Пустая() Тогда
			// Получим XML-слепок текущего состояния объекта, т.е. то состояние, которое сохранено
			// в базе данных и доступно через получение объекта по ссылке проверяемого объекта.
			записьXML.УстановитьСтроку();
			ЗаписатьXML(записьXML, объект.Ссылка.ПолучитьОбъект());
			текущееСостояниеОбъекта = ЗаписьXML.Закрыть();
		КонецЕсли;
		
		объектИзменён = текущееСостояниеОбъекта <> новоеСостояниеОбъекта;
	
	КонецЕсли; 
	
	Возврат объектИзменён;

КонецФункции // ПроверитьИзменениеОбъекта()